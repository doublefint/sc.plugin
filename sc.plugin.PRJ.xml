<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="sc.classes">
<Description>
export/import *.cls documents into/from xml files ( *.CLS.xml )</Description>
<ProcedureBlock>1</ProcedureBlock>
<Super>%RegisteredObject</Super>
<TimeCreated>63890,65741.138899</TimeCreated>

<Property name="event">
<Description>
name of studio event</Description>
</Property>

<Property name="code">
<Description>
document name ( with extension )
for example: package.class.CLS</Description>
</Property>

<Method name="codeSet">
<FormalSpec>code</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s i%code = code
	s i%name = ..nameExtract( code ) 
	s i%ext = ..extExtract( code )
	Q 1
]]></Implementation>
</Method>

<Property name="name">
<ReadOnly>1</ReadOnly>
</Property>

<Property name="ext">
<ReadOnly>1</ReadOnly>
</Property>

<Method name="fileTS">
<Description>
return file timestamp </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>file</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<Implementation><![CDATA[
	Quit:'##class(%File).Exists(file) ""
    Quit $zdt( ##class(%File).GetFileDateModified( file ), 3 )
]]></Implementation>
</Method>

<Method name="codeTS">
<Description>
Return %TimeStamp of class  </Description>
<ReturnType>%TimeStamp</ReturnType>
<Implementation><![CDATA[	Q ..ts( ..code ) ;remove ms
]]></Implementation>
</Method>

<Method name="ts">
<ClassMethod>1</ClassMethod>
<FormalSpec>class</FormalSpec>
<ReturnType>%TimeStamp</ReturnType>
<Implementation><![CDATA[	Q $p( ##class(%RoutineMgr).TS( class ), "." ) ;remove ms
]]></Implementation>
</Method>

<Method name="nameExtract">
<ClassMethod>1</ClassMethod>
<FormalSpec>code</FormalSpec>
<Implementation><![CDATA[	Q $p( code, ".", 1, $l( code, "." ) - 1 )
]]></Implementation>
</Method>

<Method name="extExtract">
<ClassMethod>1</ClassMethod>
<FormalSpec>code</FormalSpec>
<Implementation><![CDATA[	Q $p( code, ".", $l( code, "." ) )
]]></Implementation>
</Method>

<Method name="on">
<Description>
Read settings and run event handlers</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>event,code</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s ext = ..extExtract( code )
	
	s list = ##class(sc.options).handlers( ext ) ;handlers list for event
	
	s printer = ##class(sc.printer).new() ;read settings or return default
	
	if ( list = "" ) {
		
		s msg = event _ "( " _ code _" ). Warning: handlers for '" _ ext _ "' not set!"
		d printer.warn( msg )
		Q 1
		
	} 
	
	s ll = $l(list,","), class = "", result = 1
	
	for i=1:1:ll  { 
		
		s class = $p( list, ",", i )
		s handler = $zobjclassmethod( class, "%New" ) ;$zobjclassmethod -> $classmethod
		s handler.code = code, handler.event = event
		
		if '$IsObject( handler ) {
			d printer.error( "Error! Can't create handler " _ handler )
			continue  
		}
		
		s sc = handler.handle()
		
		do ##class(%SYSTEM.Status).AppendStatus( result, sc )
		
	}	
	
	Q sc
]]></Implementation>
</Method>

<Method name="handle">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
 	
 	s printer = ##class(sc.printer).new()
 
 	d printer.trace( ..event_"( "_..code _ " )" )
 	
 	Q:..ignore() 1 
	
	s filename = ..filename()
	
	if ..event = "afterdelete" {
		s sc = ..fileDelete( filename )
		d printer.w( "Delete " _ filename, sc )
		Q sc
	}
			
	#;ts - timeStamp
	s codeTS = ..codeTS()  
	d printer.trace( "codeTS: " _ codeTS )
	
	#; change events  - beforeload, aftersave, aftercompile
	s fileTS = ..fileTS( filename ) 
	d printer.trace( "fileTS: " _ fileTS _" ( " _ filename _ " ) " )
	
	
	if ( codeTS = fileTS ) Q 1 ;nothing to sync	
	
	if ( codeTS ] fileTS ) { ;export
		
		s sc = ..export( filename ) 
		d printer.w( ..code _" -> " _ filename, sc )
		Q sc
		
	}
	
	if (( fileTS ] codeTS ) && ( ..event '= "aftercompile" )) { ;import
		
		s sc = ..import( filename ) ; file newer - import from file
		d printer.w( filename _" -> " _ ..code, sc )
		Q sc
		
	}
	
	Q $$$OK
]]></Implementation>
</Method>

<Method name="workdir">
<Description>
Base directory for export</Description>
<ProcedureBlock>0</ProcedureBlock>
<Implementation><![CDATA[ Q ##class(sc.options).workdir()
]]></Implementation>
</Method>

<Method name="fileDelete">
<Description>
handler can delete file</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>file</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q ##class(%File).Delete( file )
]]></Implementation>
</Method>

<Method name="filename">
<Description>
convert code name to file name, packages translate as directories</Description>
<Implementation><![CDATA[
	
	s name = ..name
	s namelen = $l( name, "." )
	s hasPackage = ( namelen > 1 )  
	s ext = ..ext
		
	s dir = ..workdir(), path = ""
	s dm = "\" ; $$$IsWINDOWS d[eli]m[eter]
	#define dm( %dir ) $s( $e( %dir, $l( %dir ) ) = dm: "", 1: dm )
			
	if ( hasPackage ) { ;myPackage.myClass.CLS 
		
		s packages = $p( name, ".", 1, namelen - 1 ) ; for example: myPackage.myClass
		
		s path = $tr( packages, ".", dm ) ; for example: myPackage/myClass
		
		s:packages'="" dir = dir _ $$$dm(dir) _ path
		
		s name = $p( name, ".", namelen  )
		
	}
	
	s sc = ##class(%File).CreateDirectoryChain( dir ) ;need for (%File).NormalizeFilename
	s file = name _ "."_ ext _ ".xml"
	Q dir _ $$$dm(dir) _ file
]]></Implementation>
</Method>

<Method name="filemask">
<Description>
file mask for import </Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Q "*.CLS.xml"
]]></Implementation>
</Method>

<Method name="ignore">
<Description>
return true for ignore code</Description>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	Q:$e(..code)="%" 1 ;ignore system classes
	Q:$e(..code, 1, $l("INFORMATION"))="INFORMATION" 1 ;SQL-standart classes
	Q:##class(sc.legacy).codeIsGenerated(..code) 1 ;ignore generated code
	Q 0
]]></Implementation>
</Method>

<Method name="export">
<Description><![CDATA[
After export fileTS > codeTS]]></Description>
<FormalSpec>filename=..filename()</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q ##class(sc.legacy).classExport( ..code, filename )
]]></Implementation>
</Method>

<Method name="import">
<FormalSpec>filename,flags="ck-d"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s sc = $system.OBJ.Load( filename, flags, .err, .list )
	s:sc ..code = list
	Q sc
]]></Implementation>
</Method>

<Method name="exportAll">
<Description>
Initial export classes and routines</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s printer = ##class(sc.printer).new()
	
	s handler = ..%New(), handler.event = "exportAll"
    
    s rs = ..List(.sc) Q:'sc sc
    
    s result = 1
        
    while rs.Next() { 
      
      s handler.code = $g( rs.Data( "Name" ) )  ; <- this line differ from sc.classes:exportAll
      	
      if handler.ignore() continue     
	  
      s sc = handler.export()
      
      d printer.w( handler.code _ " -> " _ handler.filename(), sc )
      
      d ##class(%SYSTEM.Status).AppendStatus( result, sc )
      
    }
    
    d rs.Close()
	
	Q result
]]></Implementation>
</Method>

<Method name="List">
<Description>
Return resultset with all available documents</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&sc=1]]></FormalSpec>
<ReturnType>%ResultSet</ReturnType>
<Implementation><![CDATA[
	
	s rs = ##class(%ResultSet).%New("%Dictionary.ClassDefinition:Summary")
	s sc = rs.Execute() 
	Q rs
]]></Implementation>
</Method>

<Method name="importAll">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s workdir = ##class(sc.options).workdir()
	
	s mask = ..filemask()
	
	s sc =  ..importDir( workdir, mask, .loaded )
	
	JOB ^sc.compile(loaded)
	
	Q sc
]]></Implementation>
</Method>

<Method name="importDir">
<Description>
Caché v.5.0 can't reimport class if exist opened object</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[dir,mask,&loaded=""]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	k subdir s subdir = "", sc = 1
	
	s rs = ##class(%ResultSet).%New("%File:FileSet")
	s sc = rs.Execute( dir, mask, , 1 )
		
	while rs.Next() {
		
		s file = $g(rs.Data("Name"))
		s type = $g(rs.Data("Type"))
		s isDirectory = ( type = "D" )
		
		if ( isDirectory ){ 
			s subdir( $i( subdir ) ) = file
			continue 
		}
		
		s code = "", sc = $system.OBJ.Load( file, "k-d", , .code ) ;"k-d" - load only
		
		w !, file _ " -> " _ code, " ", +sc 
		
		if ( 'sc ) continue 
		
		s loaded = loaded _ "," _ code ;save name of loaded document
				
	} d rs.Close()
		
	#; for each subdir
	s i = "" for { s i = $o( subdir( i ), 1, dir ) Q:i=""
		s sc = ..importDir( dir, mask, .loaded ) Q:'sc
	}
		
	Q sc
]]></Implementation>
</Method>
</Class>


<Class name="sc.dfi">
<Description>
Work with *.dfi documents
%DeepSee.UserLibrary.FolderItem</Description>
<ProcedureBlock>1</ProcedureBlock>
<Super>sc.classes</Super>
<TimeCreated>63899,52452.524809</TimeCreated>

<Property name="event">
<Description>
name of studio event</Description>
</Property>

<Property name="code">
<Description>
document name ( with extension )
for example: package.class.CLS</Description>
</Property>

<Property name="id">
<ReadOnly>1</ReadOnly>
</Property>

<Property name="fullName">
<ReadOnly>1</ReadOnly>
</Property>

<Method name="codeSet">
<FormalSpec>code</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s sc = ##super(code)
	s sc = sc && ..getData( code, .data )
	if ( sc ) {
		s i%id = $g(data("id"))
		s i%fullName = $g(data("fullName"))
	}
	Q 1
]]></Implementation>
</Method>

<Property name="name">
<ReadOnly>1</ReadOnly>
</Property>

<Method name="getData">
<Description>
collect information about dfi document</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[code,&data]]></FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s sql = "Select ID, fullName From %DeepSee_UserLibrary.FolderItem Where documentName = ?"
	s rs = ##class(%SQL.Statement).%ExecDirect( , sql, code )
	s ( id, fullName ) = "" 
	if ( rs.%Next() ) {
		s data("id") = rs.ID
		s data("fullName") = rs.fullName
	}	
	Q 1
]]></Implementation>
</Method>

<Property name="ext">
<ReadOnly>1</ReadOnly>
</Property>

<Method name="filemask">
<Description>
file mask for import </Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Q "*.DFI.xml"
]]></Implementation>
</Method>

<Method name="ignore">
<Description>
return 1 for ignore code</Description>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[	Q 0
]]></Implementation>
</Method>

<Method name="export">
<Description><![CDATA[
After export fileTS > codeTS]]></Description>
<FormalSpec>filename=..filename()</FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q ..dfiExport( ..fullName, filename, 0 )
]]></Implementation>
</Method>

<Method name="dfiExport">
<ClassMethod>1</ClassMethod>
<FormalSpec>fullname,filename</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[	Q ##class(%DeepSee.UserLibrary.Utils).%Export( fullName, filename, 0 )
]]></Implementation>
</Method>

<Method name="import">
<FormalSpec>filename:%String,flags="ck-d"</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s loaded = "", sc = ..load( filename, .loaded )
	s:sc ..code = loaded( 1 ) _ ".DFI"
	
	Q sc
]]></Implementation>
</Method>

<Method name="load">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[filename,&loaded=""]]></FormalSpec>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#; .%Import( replace = 1, verbose = 0, killAll = 0, forceName = "", .loaded = "" )
	Q ##class(%DeepSee.UserLibrary.Utils).%Import( filename, 1, 0, 0, "", .loaded )
]]></Implementation>
</Method>

<Method name="codeTS">
<Description>
return local timestamp of DFI document</Description>
<ProcedureBlock>0</ProcedureBlock>
<ReturnType>%TimeStamp</ReturnType>
<Implementation><![CDATA[ Q ..ts( ..id )
]]></Implementation>
</Method>

<Method name="ts">
<ClassMethod>1</ClassMethod>
<FormalSpec>dfiid</FormalSpec>
<ReturnType>%TimeStamp</ReturnType>
<Implementation><![CDATA[
	s utcTS = ##class(%DeepSee.UserLibrary.FolderItem).timeModifiedGetStored( dfiid ) ;UTC timestamp
	s utc = $zdth(utcTS,3,,3) ;utc logical
	s local = $zdth(utc,-3) ; utc to local 
	s ts = $zdt(local,3,,0) ; local timestamp
	Q ts
]]></Implementation>
</Method>

<Method name="List">
<Description>
Return resultset with code documents</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&sc=1]]></FormalSpec>
<ReturnType>%ResultSet</ReturnType>
<Implementation><![CDATA[
	
 	s sql = "Select documentName as Name From %DeepSee_UserLibrary.FolderItem"
    s rs = ##class(%ResultSet).%New("%DynamicQuery:SQL")
    s sc = rs.Prepare( sql ) && rs.Execute( )
    Q rs
]]></Implementation>
</Method>
</Class>


<Class name="sc.options">
<Description>
options for sc.plugin
</Description>
<Abstract>1</Abstract>
<ProcedureBlock>1</ProcedureBlock>
<TimeCreated>63898,78035.606329</TimeCreated>

<Method name="Test">
<ClassMethod>1</ClassMethod>
</Method>

<Method name="storage">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Q "^sc"
]]></Implementation>
</Method>

<Method name="get">
<Description>
get settings</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>key,default=""</FormalSpec>
<Implementation><![CDATA[
	s storage = ..storage(), key = $g(key)
	Q:key="" $g(@storage, default)
	Q $g(@storage@(key), default)
]]></Implementation>
</Method>

<Method name="set">
<Description>
save settings - return settings value after save</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>key,val</FormalSpec>
<Implementation><![CDATA[
	s key = $g(key), val = $g(val)
	s storage = ..storage()
	if key = "" { 
		s @storage = val
	} else {
		s @storage@(key) = val
	}
	Q ..get(key)
]]></Implementation>
</Method>

<Method name="workdir">
<Description>
set or get working directory
by default - current namespace database directory</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>workdir</FormalSpec>
<Implementation><![CDATA[
	if $d( workdir ){
		Q ..set( "", workdir )			
	}
	Q ..get( "", $zu(12,"") )
]]></Implementation>
</Method>

<Method name="handlers">
<Description><![CDATA[
get or set handlers for event
<var>handlers</var> - comma-separated value, for example:
("aftercompile") = "sc.handler,test.sctestrunner"]]></Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>ext,handlers</FormalSpec>
<Implementation><![CDATA[
	Q:'$d(ext) ""
	
	s storage = ..storage(), key = "ext"
	
	s:$d(handlers) @storage@( key, ext ) = handlers ;set mode
	
	s handlers = $g( @storage@( key, ext ) ) ;get mode
	
	Q handlers
]]></Implementation>
</Method>

<Method name="printer">
<Description>
get or set classname for printer</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>printer</FormalSpec>
<Implementation><![CDATA[
	s storage = ..storage(), key = "printer"
	s:$d(printer) @storage@(key) = printer
	Q $g(@storage@(key))
]]></Implementation>
</Method>

<Method name="init">
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	d ..workdir( ..workdir() )
	d ..handlers( "CLS", "sc.classes" )
	d ..handlers( "MAC", "sc.routines" ) 
	d ..handlers( "INT", "sc.routines" )
	d ..handlers( "INC", "sc.routines" )
	d ..handlers( "DFI", "sc.dfi" )
	d ..printer( "sc.printer" )
	Q 1
]]></Implementation>
</Method>

<Method name="dump">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	s storage = ..storage()
	zw @storage
]]></Implementation>
</Method>
</Class>


<Class name="sc.plugin">
<Description>
[s]ource [c]ontrol plugin for InterSystems Caché (testing in v.2016.1) 
Just export or import to/from filesystem. Nothing more.
</Description>
<ProcedureBlock>1</ProcedureBlock>
<Super>%Studio.SourceControl.Base</Super>
<TimeCreated>63294,56897.842943</TimeCreated>

<Method name="Test">
<ClassMethod>1</ClassMethod>
</Method>

<Parameter name="Flags">
<Description><![CDATA[
Just sync.
<li>1 : Present dialog on check in</li>
<li>2 : Present dialog on check out</li>
<li>3 : Ask to add new item to source control</li>
<li>4 : Ask to remove deleted item from source control</li></ul>]]></Description>
<Type>STRING</Type>
<Default>0000</Default>
</Parameter>

<Property name="Modified">
<Description>
Workaround for error in %Studio.AbstractDocument:SaveStream()+16 
in Cache for Windows (x86-64) 2016.1 (Build 596U) Mon Nov 9 2015 21:12:55 EST

If the root node is set to 1 then calls to the source control hooks will keep track if the
source control hook modifies the document and if it does then it will defined
%SourceControl.Modified(docname)=1.</Description>
<Type>%String</Type>
<MultiDimensional>1</MultiDimensional>
</Property>

<Method name="OnBeforeLoad">
<Description>
Import the file from the external format into Cache.
This is called before the actual load of data to give the chance
to load the item from an external format.</Description>
<FormalSpec>code:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	s ..Modified = 0 ;workaround for error in %Studio.AbstractDocument:SaveStream()+16 
    Q ..on( "beforeload", code )
]]></Implementation>
</Method>

<Method name="OnAfterSave">
<Description>
Called when Studio save something</Description>
<FormalSpec>code:%String,obj:%RegisteredObject=$$$NULLOREF</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[    Q ..on( "aftersave" , code )
]]></Implementation>
</Method>

<Method name="OnAfterCompile">
<Description>
Called when Studio compile code 
and some parts of code may be changed
for example - storage scheme</Description>
<FormalSpec>code:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[    Q ..on( "aftercompile", code ) ;only for additional export
]]></Implementation>
</Method>

<Method name="OnAfterDelete">
<Description>
Called when Studio delete code</Description>
<FormalSpec>code:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[ 	Q ..on( "afterdelete", code )
]]></Implementation>
</Method>

<Method name="on">
<ClassMethod>1</ClassMethod>
<FormalSpec>event,code</FormalSpec>
<Implementation><![CDATA[	Q ##class(sc.classes).on( event, code )
]]></Implementation>
</Method>

<Method name="install">
<ClassMethod>1</ClassMethod>
<FormalSpec>ns=$zu(5)</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s classname = ..%ClassName(1) ;legacy equivalent of $classname()
	d ##class(sc.legacy).sourceControlClass( classname )
	
	d ##class(sc.options).handlers( "CLS", "sc.classes" )
	d ##class(sc.options).handlers( "MAC", "sc.routines" ) 
	d ##class(sc.options).handlers( "INT", "sc.routines" )
	d ##class(sc.options).handlers( "INC", "sc.routines" )
	d ##class(sc.options).handlers( "DFI", "sc.dfi" )
	d ##class(sc.options).printer( "sc.printer" )
	
	Q 1
]]></Implementation>
</Method>

<Method name="uninstall">
<ClassMethod>1</ClassMethod>
<FormalSpec>ns=$zu(5)</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	d ##class(sc.legacy).sourceControlClass( "" )
	Q 1
]]></Implementation>
</Method>
</Class>


<Class name="sc.printer">
<Description>
Output messages </Description>
<ProcedureBlock>1</ProcedureBlock>
<Super>%RegisteredObject</Super>
<TimeCreated>63890,50155.078667</TimeCreated>

<Parameter name="LOG">
<Description>
1 - error, 2 - warning, 3 - trace </Description>
<Default>3</Default>
</Parameter>

<Property name="device">
<InitialExpression>$io</InitialExpression>
</Property>

<Method name="%OnNew">
<Description><![CDATA[
This callback method is invoked by the <METHOD>%New</METHOD> method to 
provide notification that a new instance of an object is being created.]]></Description>
<FormalSpec>device=$io</FormalSpec>
<Private>1</Private>
<ReturnType>%Status</ReturnType>
<ServerOnly>1</ServerOnly>
<Implementation><![CDATA[
	s ..device = $g(device,$io)
	Q 1
]]></Implementation>
</Method>

<Method name="new">
<ClassMethod>1</ClassMethod>
<ReturnType>printer</ReturnType>
<Implementation><![CDATA[
	
	s printer = ..%New($io) 
	
	s custom = ##class(options).printer()
	
	Q:custom="" printer
	
	s $zt = "newErr"
	
	s printer = $zobjclassmethod( custom, "%New" )
	
	Q printer
	
newErr
	s $zt = "", $ze = "" 
	Q ..%New()
]]></Implementation>
</Method>

<Method name="w">
<FormalSpec>msg,sc=1</FormalSpec>
<Implementation><![CDATA[
	s io = $io	

	s status = "Ok!", err = "", elist = ""
	
	if 'sc {
		
		s status = "FAIL :( "
		
		d ##class(%SYSTEM.Status).DecomposeStatus(sc, .elist)
		
		s status = status _ $g(elist(1))
		
	}
	
	use ..device w !,"* ", msg, " ", status
	
	use io
]]></Implementation>
</Method>

<Method name="error">
<FormalSpec>msg</FormalSpec>
<Implementation><![CDATA[
	
	d ..w(msg)
]]></Implementation>
</Method>

<Method name="warn">
<FormalSpec>msg</FormalSpec>
<Implementation><![CDATA[	d:(..#LOG>=2) ..w(msg)
]]></Implementation>
</Method>

<Method name="trace">
<FormalSpec>msg</FormalSpec>
<Implementation><![CDATA[	d:(..#LOG>=3) ..w(msg)
]]></Implementation>
</Method>
</Class>


<Class name="sc.routines">
<Description>
For MAC,INT,INC</Description>
<ProcedureBlock>1</ProcedureBlock>
<Super>sc.classes</Super>
<TimeCreated>63902,60823.714383</TimeCreated>

<Method name="filemask">
<Description>
file mask for import </Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[	Q "*.MAC.xml;*.INT.xml;*.INC.xml"
]]></Implementation>
</Method>

<Method name="exportAll">
<Description>
Initial export classes and routines</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	
	s printer = ##class(sc.printer).new()
	
	s handler = ..%New(), handler.event = "exportAll" 
        
    s result = 1, rs = ..List(.sc) Q:'sc sc 
    
    while rs.Next() { 
      
      s handler.code = $g(rs.Data("Name"))
      	
      if handler.ignore() continue     
	  
      s sc = handler.export()
      
      d printer.w( handler.code _ " -> " _ handler.filename(), sc )
      
      d ##class(%SYSTEM.Status).AppendStatus( result, sc )
      
    }
    
    d rs.Close()
	
	Q result
]]></Implementation>
</Method>

<Method name="List">
<Description>
Return resultset with code documents</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[&sc=1]]></FormalSpec>
<ReturnType>%ResultSet</ReturnType>
<Implementation><![CDATA[
	s rs = ##class(%ResultSet).%New("%Routine:RoutineList")
	s sc = rs.Execute() 
    Q rs
]]></Implementation>
</Method>

<Method name="ignore">
<Description>
return true for ignore code</Description>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	Q:$e(..code)="%" 1 ;ignore system routines
	Q:##class(sc.legacy).codeIsGenerated(..code) 1 ;ignore generated code
	Q 0
]]></Implementation>
</Method>
</Class>


<Project name="sc_plugin" LastModified="2015-12-30 16:07:04.540148">
  <Items>
    <ProjectItem name="sc.classes" type="CLS"></ProjectItem>
    <ProjectItem name="sc.dfi" type="CLS"></ProjectItem>
    <ProjectItem name="sc.options" type="CLS"></ProjectItem>
    <ProjectItem name="sc.plugin" type="CLS"></ProjectItem>
    <ProjectItem name="sc.printer" type="CLS"></ProjectItem>
    <ProjectItem name="sc.routines" type="CLS"></ProjectItem>
  </Items>
</Project>
</Export>
